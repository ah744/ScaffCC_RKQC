/***********************************************
* 
* SHA-1 Encryption Function
*
************************************************/



/*-------------------------------------Helper functions-----------------------------------*/

//Tweaked pseudo-classical gates
// 32 control Toffoli
ctqg nToffoli(qbit target[32], qbit in1[32], qbit in2[32]){
    //This should not lead to a compile error as long as the values are set in compile time.
    zero_to_garbage ancilla[32];
    int i;
  for (i=0; i<32; i++){  
    $ toffoli(in1[i], in2[i], ancilla[i]);
  }
  $ target := ancilla;
}

ctqg AND(qbit target[32], qbit in1[32], qbit in2[32]){
  $ nToffoli(target, in1, in2);
}

ctqg NAND(qbit target[32], qbit in1[32], qbit in2[32]){
  int i;
  $ AND(target, in1, in2);
  for(i=0; i<32; i++){ //forall
      $ not(target[i]);
    }
}

ctqg NOR(qbit target[32], qbit in1[32], qbit in2[32]){
    int i;
    zero_to_garbage temp1[32];
    zero_to_garbage temp2[32];
    $ temp1 := in1;
    $ temp2 := in2;
    for(i=0; i<32; i++){ //forall
      $ not(temp1[i]);
    }
    for(i=0; i<32; i++){ //forall
      $ not(temp2[i]);
    }    
  $ AND(target, temp1, temp2);
}

ctqg OR(qbit target[32], qbit in1[32], qbit in2[32]){
  int i;
  $ NOR(target, in1, in2);
  for(i=0; i<32; i++){ //forall
      $ not(target[i]);
  }
}

ctqg XOR(qbit target[32], qbit in1[32], qbit in2[32]){
  /*OR(AND(target, NOT(in)), AND(NOT(target), in));*/
  zero_to_garbage temp1[32];
  zero_to_garbage temp2[32];
  zero_to_garbage temp3[32];
  zero_to_garbage temp4[32];
  int i;
  $ temp1 := in1;
  $ temp2 := in2;

  for(i=0; i<32; i++){ //forall
      $ not(temp2[i]); 
  }

  $ AND(temp3, temp1, temp2);

  for(i=0; i<32; i++){ //forall
      $ not(temp2[i]);
  }
  for(i=0; i<32; i++){ //forall
      $ not(temp1[i]);
  }

  $ AND(temp4, temp1, temp2);
  $ OR(target, temp3, temp4);
}

ctqg <n> reverseBits(qbit a[n]){
  zero_to_garbage temp[n];
  $ temp := a;
  int i;
  INSTANTIATE_MODULE
    for (i=0; i<n; i++){
      MAP_SIGNAL(%%b[i], @temp[n-1-i]);
    }
    for (i=0; i<n; i++){
      MAP_SIGNAL(%%a[i], @a[i]);
    }
  assign_value_of_b_to_a(LOCATION_INFO, n);
}

ctqg leftrotate(qbit a[32]){
  /*32 bit leftshift arithmetic by 1 bit, loop for multibit */
  int i;
  zero_to_garbage temp[32];
  $ temp := a;
  INSTANTIATE_MODULE
  MAP_SIGNAL(%%b[i], @temp[31]);
  for (i=1; i<32; i++){
    MAP_SIGNAL(%%b[i], @temp[i-1]);
  }
  for (i=0; i<32; i++){
    MAP_SIGNAL(%%a[i], @a[i]);
  }
  assign_value_of_b_to_a(LOCATION_INFO, 32);
}

ctqg leftshift(qbit a[32]){
  /*32 bit leftshift arithmetic by 1 bit, loop for multibit */
  int i;
  zero_to_garbage temp[32];
  zero_to_garbage zero[32];
  $ zero := 0;
  $ temp := a;
  INSTANTIATE_MODULE
  for (i=0; i<32; i++){
    if (i == 0){
      MAP_SIGNAL(%%b[i], @zero[31]);
    }
    else{
      MAP_SIGNAL(%%b[i], @temp[i-1]);
    }
  }
  for (i=0; i<32; i++){
    MAP_SIGNAL(%%a[i], @a[i]);
  }
  assign_value_of_b_to_a(LOCATION_INFO, 32);
}

ctqg pad(qbit a[1024], qbit input[448], qbit padding[576]){

  $ padding := "100000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000111000000"; 
  $ [576] reverseBits(padding);
  int i;
  INSTANTIATE_MODULE
    for (i=1023; i>575; i--){
      MAP_SIGNAL(%%b[i], @input[i-576]);
    }
    for (i=575; i>=0; i--){
      MAP_SIGNAL(%%b[i], @padding[i]);
    }
    for (i=0; i<1024; i++){
      MAP_SIGNAL(%%a[i], @a[i]);
    }
  assign_value_of_b_to_a(LOCATION_INFO, 1024);
}

/* Mapping Function */
ctqg <n> map(qbit a[32], qbit in[512]){  
  int i;
  int j;

  INSTANTIATE_MODULE
    for(i=0; i<32; i++){
      MAP_SIGNAL(%%b[i], @in[i + (32 * (15 - n))])
    }
    for (i = 0; i < 32; i++){
      MAP_SIGNAL(%%a[i], @a[i]);
    }
  assign_value_of_b_to_a(LOCATION_INFO, 32); 
}

/* Create final hash, big endian bytes created with bit addressing */
ctqg finalHash(qbit a[160], qbit h0[32], qbit h1[32], qbit h2[32], qbit h3[32], qbit h4[32]){
  int i;
  int j;

  INSTANTIATE_MODULE
    for(i=0; i<32; i++){
      MAP_SIGNAL(%%b[i], @h4[i]);
    }
    for(i=32; i<64; i++){
      MAP_SIGNAL(%%b[i], @h3[(i-32)]);
    }
    for(i=64; i<96; i++){
      MAP_SIGNAL(%%b[i], @h2[(i-64)]);
    }
    for(i=96; i<128; i++){
      MAP_SIGNAL(%%b[i], @h1[(i-96)]);
    }
    for(i=128; i<160; i++){
      MAP_SIGNAL(%%b[i], @h0[(i-128)]);
    }
    for (i=0; i<160; i++){
      MAP_SIGNAL(%%a[i], @a[i]);
    }
    assign_value_of_b_to_a(LOCATION_INFO, 160);
}

ctqg firstBlock(qbit a[512], qbit in[1024]){
  int i;
  INSTANTIATE_MODULE
    for(i=511; i>=0; i--){
      MAP_SIGNAL(%%b[i], @in[i+512]);
    }
    for(i=0; i<512; i++){
      MAP_SIGNAL(%%a[i], @a[i]);
    }
  assign_value_of_b_to_a(LOCATION_INFO, 512);
}

ctqg secondBlock(qbit a[512], qbit in[1024]){
  int i;
  INSTANTIATE_MODULE
    for(i=511; i>=0; i--){
      MAP_SIGNAL(%%b[i], @in[i]);
    }
    for(i=0; i<512; i++){
      MAP_SIGNAL(%%a[i], @a[i]);
    }
  assign_value_of_b_to_a(LOCATION_INFO, 512);
}


/*---------------------------------------------Encryption ctqg---------------------------------------------------------------*/

ctqg encrypt( qint[448] input, qint[160] hash ) {

  //padding specified by SHA1
  zero_to_garbage padding[576];

  //input concatenated with pre-processing "padding" as specified by SHA1
  zero_to_garbage paddedInput[1024]; 

  //Constants, also used for computation within the main blocks
  zero_to_garbage h0[32];
  zero_to_garbage h1[32];
  zero_to_garbage h2[32];
  zero_to_garbage h3[32]; 
  zero_to_garbage h4[32];
  zero_to_garbage A[32];
  zero_to_garbage B[32]; 
  zero_to_garbage C[32];  
  zero_to_garbage D[32]; 
  zero_to_garbage E[32];
  zero_to_garbage K1[32];
  zero_to_garbage K2[32];
  zero_to_garbage K3[32];
  zero_to_garbage K4[32];
  zero_to_garbage f[32];
  zero_to_garbage a[32];
  zero_to_garbage b[32];

  //temporaries used for computation
  zero_to_garbage temp[32];
  zero_to_garbage temp1[32];
  zero_to_garbage temp2[32];
  zero_to_garbage temp3[32];
  zero_to_garbage temp4[32];
  zero_to_garbage temp5[32];
  zero_to_garbage target[32];

  //Holder for SHA1 mysterious constants
  zero_to_garbage k[32];

  //Split input into two 512 bit blocks
  zero_to_garbage block[2][512];

  //Counters
  int i;
  int t;
  int j;

  //Initialize the constants for the rounds of SHA1, binary reversed due to bit indexing

  $ h0 := "01100111010001010010001100000001"; //0x67452301;
  $ [32] reverseBits(h0);
  $ h1 := "11101111110011011010101110001001"; //0xEFCDAB89;
  $ [32] reverseBits(h1);
  $ h2 := "10011000101110101101110011111110"; //0x98BADCFE;
  $ [32] reverseBits(h2);
  $ h3 := "00010000001100100101010001110110"; //0x10325476;
  $ [32] reverseBits(h3);
  $ h4 := "11000011110100101110000111110000"; //0xC3D2E1F0;
  $ [32] reverseBits(h4);

  $ A := "01100111010001010010001100000001"; //0x67452301;
  $ [32] reverseBits(A);
  $ B := "11101111110011011010101110001001"; //0xEFCDAB89;
  $ [32] reverseBits(B);
  $ C := "10011000101110101101110011111110"; //0x98BADCFE;
  $ [32] reverseBits(C);
  $ D := "00010000001100100101010001110110"; //0x10325476;
  $ [32] reverseBits(D);
  $ E := "11000011110100101110000111110000"; //0xC3D2E1F0;
  $ [32] reverseBits(E);

  $ K1 := "01011010100000100111100110011001"; //0x5A827999;
  $ [32] reverseBits(K1);
  $ K2 := "01101110110110011110101110100001"; //0x6ED9EBA1;
  $ [32] reverseBits(K2);
  $ K3 := "10001111000110111011110011011100"; //0x8F1BBCDC;
  $ [32] reverseBits(K3);
  $ K4 := "11001010011000101100000111010110"; //0xCA62C1D6;
  $ [32] reverseBits(K4);



  /* Pre-processing: pad the input 
  Concatenate 384 to satisfy paddedInput is congruent to 512
  Input is 16 bytes, so padding is always 320 bits + 64 bits == 384 bits */

  $ pad(paddedInput, input, padding);
  $ firstBlock(block[0], paddedInput);
  $ secondBlock(block[1], paddedInput);

  zero_to_garbage  W[80][32];
 

 /* Main Loop: Once for each 512 bit block of the original message */
  for (j=0; j<2; j++){
  /*Processing: split message into 16 32-bit words*/
    if(j!=0){
      $ A := h0;
      $ B := h1;
      $ C := h2;
      $ D := h3;
      $ E := h4;
    }

    for (i=0; i<16; i++){
      $ [i] map(W[i], block[j]);
    }

    /*Extend the 16 words into 80 words*/

    for (i=16; i<80; i++){
    /*$ W[i] := leftrotate(1, XOR(XOR(XOR(W[i-3], W[i-8]), W[i-14]), W[i-16]);*/
      $ XOR(temp2, W[i-3], W[i-8]);
      $ XOR(temp3, temp2, W[i-14]);
      $ XOR(target, temp3, W[i-16]);
      $ leftrotate(target);
      $ W[i] := target;
    }

    for(t=0; t<80; t++){

    /*----------Part 1: First 20 Rounds----------*/
      if(t<20){
    /*$ f := OR(AND(B, C), AND(not(B), D));*/
        $ temp3 := B;
        $ AND(temp, B, C);
        for(i=0; i<32; i++){ //forall
          $ not(temp3[i]);
        }
        $ AND(temp1, temp3, D);
        $ XOR(f, temp, temp1);
        $ k := K1;
      }

    /*----------Part 2: Second 20 Rounds----------*/
      else if(t<40 && t>=20){
    /*$ f := XOR(XOR(B, C), D);*/
        $ XOR(temp, B, C);
        $ XOR(f, temp, D);
        $ k := K2;
      }

    /*----------Part 3: Third 20 Rounds----------*/
      else if(t<60 && t>=40){
    /*$ f := OR(OR(AND(B,C), AND(B,D)), AND(C,D));*/
        $ AND(temp1, B, C);
        $ AND(temp2, B, D);
        $ XOR(temp3, temp1, temp2);
        $ AND(temp4, C, D);
        $ XOR(f, temp3, temp4); 
        $ k := K3;
      }

    /*----------Part 4: Last 20 Rounds----------*/
      else if(t<80 && t>=60){
    /*$ f := XOR(XOR(B, C), D);*/
        $ XOR(temp, B, C);
        $ XOR(f, temp, D);
        $ k := K4;
      }

      $ temp1 := A;
      $ temp2 := B;
    /*$ temp := (leftrotate(5, A) + f + E + kt + W[t]);*/
      for(i=0; i<5; i++){
        $ leftrotate(temp1);
      }

      $ temp := temp1;
      $ temp += f;  
      $ temp += E;
      $ temp += k;
      $ temp += W[t];

      $ E := D;
      $ D := C;

      for(i=0; i<30; i++){
        $ leftrotate(temp2);
      }

      $ C := temp2;
      $ B := A;
      $ A := temp;
    }

    $ h0 += A;
    $ h1 += B;
    $ h2 += C;
    $ h3 += D;
    $ h4 += E;
  }
  
  //Final hash big endian
  /*$ hh := OR(OR(OR(OR(leftshift(128, h0), leftshift(96, h1)), leftshift(64, h2)), leftshift(32, h3)), h4), done with signal mapping*/
  $ finalHash(hash, h0, h1, h2, h3, h4);
}

int main (int argc, char** argv){
  qbit x[448];
  qbit hash[160];
  encrypt(x, hash);
  return 0;
}
